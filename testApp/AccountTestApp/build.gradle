buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric' // must be after com.android.application

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    api project(':lib:AccountLib')
    api project(':lib:AndroidUtils')

    api "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    api 'com.squareup:otto:1.3.8'
    annotationProcessor 'com.jakewharton:butterknife:6.1.0'
    api 'com.jakewharton:butterknife:6.1.0'
    api 'com.squareup.retrofit:retrofit-mock:1.9.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    androidTestImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    api "com.google.android.gms:play-services-auth:${rootProject.ext.googlePlayServicesVersion}"

    api('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true;
    }

    androidTestImplementation ("com.android.support.test.espresso:espresso-core:2.2") {
        exclude group: 'com.android.support'
    }
    androidTestImplementation ("com.android.support.test.espresso:espresso-contrib:2.0") {
        exclude group: 'com.android.support'
    }
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

def calculateVersionCode = { ->
    return versionMajor * 10000 + versionMinor * 100 + versionPatch
}

def calculateVersionName = { ->
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    flavorDimensions "default"

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    defaultConfig {
        applicationId "com.expedia.account.sample"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode calculateVersionCode()
        versionName calculateVersionName()
        testApplicationId "com.expedia.account.test"
        testInstrumentationRunner "com.expedia.account.test.utils.InstrumentationTestRunner"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    buildTypes {
        debug {
            ext.betaDistributionReleaseNotesFilePath = "changelog.txt"
            ext.betaDistributionGroupAliasesFilePath = "scripts/ci/crashlytics_beta_groups.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

