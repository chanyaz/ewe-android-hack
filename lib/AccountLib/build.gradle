apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

version = '1.2.8'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    resourcePrefix 'acct__'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

dependencies {
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"

    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:retrofit-mock:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.squareup.phrase:phrase:1.1.0'

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.squareup:otto:1.3.8'

    compile 'com.facebook.android:facebook-android-sdk:4.8.1'

    compile 'com.jakewharton.timber:timber:3.1.0'
    compile "com.google.android.gms:play-services-auth:${rootProject.ext.googlePlayServicesVersion}"

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.1'
    testCompile 'org.assertj:assertj-core:1.7.1'
    testCompile 'org.mockito:mockito-core:2.0.13-beta'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

// See https://groups.google.com/forum/#!topic/adt-dev/kzbEHAGbFVg
afterEvaluate {
    apply plugin: 'maven-publish'

    publishing {
        publications {
            lib(MavenPublication) {
                groupId 'com.expedia'
                artifactId 'android-account-lib'
                version project.version

                artifact(sourceJar)
                artifact(project.tasks.bundleRelease.archivePath)

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.compile.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }

        repositories {
            maven {
                def path = project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'
                url "${project.hasProperty("REPOSITORY_PATH") ? REPOSITORY_PATH : "$buildDir/repo"}/${path}"
            }
        }
    }
}
