def shouldRunProguard = GradleUtil.shouldEnableProguard(project)

apply plugin: 'android'
apply plugin: AndroidExpediaPlugin

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: [
        'activeandroid-20131111.jar',
        'admsAppLibrary-3.0.5.jar',
        'adxtag2.4.5exp.jar',
    ])
    compile project(':lib:FacebookSdk:facebook')
    compile project(':lib:FlightLib')
    compile project(':lib:google-play-services')
    compile project(':lib:svg-android:svgandroid')
    compile project(':lib:Utils')
    compile project(':lib:ShareEverywhere:shareeverywhere')
    compile project(':lib:AndroidTime')

    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.google.code.gson:gson:2.2.4'

    androidTestCompile fileTree('../lib/TestUtils/project/libs').include(
        'robotium-solo-5.0.1.jar',
    )
    androidTestCompile 'com.jakewharton.espresso:espresso:1.1-r3'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.1'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    lintOptions {
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    defaultConfig {
        versionCode 33
        versionName "3.5.1"
        minSdkVersion 15
        targetSdkVersion 19

        testPackageName "com.expedia.bookings.test"
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"

        buildConfigField "boolean", "IS_EXPEDIA", "false"
        buildConfigField "boolean", "IS_VSC", "false"
        buildConfigField "boolean", "IS_TRAVELOCITY", "false"
        buildConfigField "boolean", "IS_AUTOMATION", "false"
        buildConfigField "String", "MEDIA_URL", "\"http://media.expedia.com\""
    }

    productFlavors {
        Expedia {
            buildConfigField "boolean", "IS_EXPEDIA", "true"
            buildConfigField "String", "MEDIA_URL", "\"http://media.expedia.com\""
            packageName "com.expedia.bookings"
        }

        VSC {
            buildConfigField "boolean", "IS_VSC", "true"
            buildConfigField "String", "MEDIA_URL", "\"http://media.expedia.com\""
            packageName "com.expedia.bookings.vsc"
            versionCode 1
            versionName "1.0"
        }

        Travelocity {
            buildConfigField "boolean", "IS_TRAVELOCITY", "true"
            buildConfigField "String", "MEDIA_URL", "\"http://media.travelocitycustomercare.com\""
            packageName "com.travelocity.android"
            versionCode 1
            versionName "1.0"
        }

        ExpediaAutomation {
            buildConfigField "boolean", "IS_EXPEDIA", "true"
            buildConfigField "boolean", "IS_AUTOMATION", "true"
            packageName "com.expedia.bookings.auto"
            testInstrumentationRunner "com.expedia.bookings.test.utils.SpoonInstrumentationTestRunner"
        }

        TravelocityAutomation {
            buildConfigField "boolean", "IS_TRAVELOCITY", "true"
            buildConfigField "boolean", "IS_AUTOMATION", "true"
            packageName "com.travelocity.android.auto"
            versionCode 1
            versionName "1.0"
        }
    }

    // Add the HockeyMeta extension to all build types
    buildTypes.all { buildType ->
        buildType.extensions.create("hockey", HockeyExtension)
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"

            runProguard shouldRunProguard
            proguardFile 'proguard.cfg'
        }

        latest.initWith(buildTypes.debug)
        latest {
            packageNameSuffix ".latest"
            hockey.enabled true
        }

        next.initWith(buildTypes.debug)
        next {
            packageNameSuffix ".next"
            hockey.enabled true
        }

        release {
            runProguard true
            proguardFile 'proguard.cfg'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        debug {
            manifest.srcFile 'AndroidManifest-debug.xml'
        }

        androidTest {
            java.srcDirs = ['unitTests/java']
            res.srcDirs = ['unitTests/res']
        }

        androidTestExpediaAutomation {
            manifest.srcFile 'tests/AndroidManifest.xml'
            java.srcDirs = ['tests/src']
            res.srcDirs = ['tests/res']
        }

        androidTestTravelocityAutomation {
            manifest.srcFile 'tests/AndroidManifest-travelocity.xml'
            java.srcDirs = ['tests/src']
            res.srcDirs = ['tests/res_travelocity']
        }

        Expedia {
            manifest.srcFile 'AndroidManifest-expedia.xml'
        }

        VSC {
            manifest.srcFile 'AndroidManifest-vsc.xml'
            res.srcDirs 'res_vsc'
        }

        Travelocity {
            manifest.srcFile 'AndroidManifest-travelocity.xml'
            res.srcDirs 'res_travelocity'
        }

        ExpediaAutomation {
            manifest.srcFile 'AndroidManifest-expedia.xml'
        }

        TravelocityAutomation {
            manifest.srcFile 'AndroidManifest-travelocity.xml'
            res.srcDirs 'res_travelocity'
        }

        signingConfigs {
            debug {
                storeFile file("../common/debug.keystore")
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}
