def shouldRunProguard = GradleUtil.shouldEnableProguard(project)
def gitRev = GradleUtil.getGitRevision()

def CHANGELOG_PATH = "changelog.txt"
def CRASHLYTICS_BETA_GROUPS_PATH = "buildbot/crashlytics_beta_groups.txt"

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'
apply plugin: 'fork'
apply from: '../jacoco.gradle'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://www.leanplum.com/leanplum-sdks/' }
    maven {
        url "s3://mobiata-maven/release"
        credentials(AwsCredentials) {
            accessKey AWS_ACCESS_KEY
            secretKey AWS_SECRET_KEY
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: [
            'activeandroid-20131111.jar',
            'ADMS_AppLibrary-3.2.7-Android.jar',
            'MobileAppTracker-3.11.4.jar',
    ])
    compile project(':lib:AndroidTime')
    debugCompile project(':lib:bug-reporter:bugshaker')
    // We keep some classes in a pure java project so we can test them with pure jvm unit tests
    compile (project(':lib:ExpediaBookings')) {
        exclude group: 'org.json'
    }
    compile 'com.expedia:svg-android:0.1.0'
    compile 'com.expedia:android-account-lib:1.1.12'
    compile 'com.mobiata:android-utils:1.0.4'
    compile 'com.dgmltn:share-everywhere:1.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    testCompile "org.jetbrains.kotlin:kotlin-test:${kotlin_version}"
    compile 'com.google.maps.android:android-maps-utils:0.4.3'

    // Google Play Services deps on the support lib
    // however we add it here so the android-sdk-manager plugin behaves

    compile "com.android.support:support-v4:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:palette-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:gridlayout-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:support-annotations:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"

    compile "com.google.android.gms:play-services-maps:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services-wallet:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services-location:${rootProject.ext.googlePlayServicesVersion}"

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile "com.squareup.okhttp3:okhttp:${okhttp_version}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttp_version}"
    compile "com.squareup.okhttp3:okhttp-urlconnection:${okhttp_version}"
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile 'com.squareup.okio:okio:1.7.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.phrase:phrase:1.1.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.leanplum:Leanplum:1.2.24'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile "io.reactivex:rxjava:${rxjava_version}"
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.facebook.android:facebook-android-sdk:4.16.1'
    compile 'io.card:android-sdk:5.3.4'
    compile ('com.wefika:flowlayout:0.4.0') {
        exclude group: 'com.intellij'
    }

    // Dagger 2.6.1
    compile 'com.google.dagger:dagger:2.6.1'
    apt 'com.google.dagger:dagger-compiler:2.6.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile ('com.crashlytics.sdk.android:crashlytics:2.5.7@aar') {
        transitive = true
    }

    // Debug dependencies
//    def leakCanaryVersion = '1.3.1'
//    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
//    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"

    // junit is only for mockwebserver
    debugCompile "com.squareup.okhttp3:mockwebserver:${okhttp_version}"
    debugCompile 'junit:junit:4.12'
    debugCompile (project(':lib:mocked:mocke3')) {
        transitive = false
    }

    def stethoVersion = '1.3.1'
    debugCompile "com.facebook.stetho:stetho:${stethoVersion}"
    debugCompile "com.facebook.stetho:stetho-okhttp3:${stethoVersion}"

    // Test dependencies
    testCompile 'junit:junit:4.12'
    def roboVersion = '3.0'
    testCompile "org.robolectric:robolectric:${roboVersion}"
    testCompile "org.robolectric:shadows-multidex:${roboVersion}"
    testCompile 'org.mockito:mockito-core:2.0.31-beta'

    def espressoVersion = '2.2.1'
    androidTestCompile ("com.android.support.test.espresso:espresso-core:${espressoVersion}"){
        exclude group: 'com.android.support'
        exclude group: 'javax.inject'
    }
    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:${espressoVersion}"){
        exclude group: 'com.android.support'
        exclude group: 'javax.inject'
    }

    androidTestCompile ('com.android.support.test:rules:0.4.1') {
        exclude group: 'com.android.support'
    }
    androidTestCompile ("com.android.support.test.espresso:espresso-intents:${espressoVersion}") {
        exclude group: 'com.android.support'
    }

    androidTestCompile fileTree(dir: 'libs', include: [
            'spoon-client-1.1.3-EXP.jar',
    ])
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:1.2"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    useLibrary 'org.apache.http.legacy'

    dexOptions {
        javaMaxHeapSize "2g"
    }

    lintOptions {
        checkReleaseBuilds false
        showAll true
        lintConfig file("lint.xml")
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        debug {
            storeFile file("../common/debug.keystore")
        }

        release {
            /*
             We will provide value to these keys using -P commandline option of gradle
            For Example,./gradlew assemleExpediaRelease
            -PRELEASE_STORE_PASSWORD='Expedia_RELEASE_STORE_PASSWORD'
            -PRELEASE_KEY_PASSWORD='Expedia_RELEASE_KEY_PASSWORD'
            -PRELEASE_STORE_FILE='Expedia_RELEASE_STORE_FILE'
            -PRELEASE_KEY_ALIAS='Expedia_RELEASE_KEY_ALIAS'
            'Expedia_RELEASE_STORE_PASSWORD' etc will point to value in gradle.properties.
            */
            storeFile file(GradleUtil.getPropertyWithDefault(project, RELEASE_STORE_FILE, "."))
            storePassword GradleUtil.getPropertyWithDefault(project, RELEASE_STORE_PASSWORD, "")
            keyAlias GradleUtil.getPropertyWithDefault(project, RELEASE_KEY_ALIAS, "")
            keyPassword GradleUtil.getPropertyWithDefault(project, RELEASE_KEY_PASSWORD, "")
        }
    }

    defaultConfig {
        versionCode 1
        versionName "undefined"

        minSdkVersion 19
        targetSdkVersion 23

        multiDexEnabled = true
        testApplicationId "com.expedia.bookings.test"
        testInstrumentationRunner "com.expedia.bookings.test.espresso.InstrumentationTestRunner"

        buildConfigField "String", "GIT_REVISION",  "\"${gitRev}\""
        buildConfigField "String", "BUILD_NUMBER",  "\"${System.env.BUILD_NUMBER}\""

        // Explicitly define the languages we use so GPS and other libraries doesn't include unnecessary ones
        resConfigs "da", "de", "en_CA", "en_GB", "es", "es_ES", "fi", "fr", "fr_CA", "in", "it", "ja", "ko", "nb", "nl", "pt_BR", "sv", "th", "vi", "zh", "zh_HK", "zh_TW"
    }

    productFlavors {
        expedia {
            buildConfigField "String", "brand", "\"Expedia\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "com.expedia.bookings"

            def major = 7
            def minor = 9
            def patch = 0
            def build = 0

            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        ebookers {
            buildConfigField "String", "brand", "\"ebookers\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "com.ebookers"

            def major = 5
            def minor = 4
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        voyages {
            buildConfigField "String", "brand", "\"Agence Voyages-SNCF.com\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "com.expedia.bookings.vsc"

            def major = 3
            def minor = 2
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        travelocity {
            buildConfigField "String", "brand", "\"Travelocity\""
            buildConfigField "String", "MEDIA_URL", "\"https://images.trvl-media.com\""
            applicationId "com.travelocity.android"

            def major = 8
            def minor = 6
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        airAsiaGo {
            buildConfigField "String", "brand", "\"AirAsiaGo\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "com.airasiago.android"
            def major = 3
            def minor = 2
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        wotif {
            buildConfigField "String", "brand", "\"Wotif\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "com.wotif.android"
            def major = 3
            def minor = 5
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        lastMinute {
            buildConfigField "String", "brand", "\"LM AU & NZ\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "com.lastminute.android"
            def major = 3
            def minor = 5
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        samsung {
            buildConfigField "String", "brand", "\"Expedia\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "com.samsung.travel.android"

            def major = 1
            def minor = 2
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        orbitz {
            buildConfigField "String", "brand", "\"Orbitz\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "com.orbitz"

            def major = 7
            def minor = 3
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        cheapTickets {
            buildConfigField "String", "brand", "\"CheapTickets\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "com.cheaptickets"

            def major = 5
            def minor = 3
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }

        mrJet {
            buildConfigField "String", "brand", "\"MrJet\""
            buildConfigField "String", "MEDIA_URL", "\"https://media.expedia.com\""
            applicationId "se.mrjet"

            def major = 5
            def minor = 3
            def patch = 0
            def build = 0
            versionCode GradleUtil.versionCode(major, minor, patch, build)
            versionName GradleUtil.versionName(major, minor, patch, build)
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true

            applicationIdSuffix ("." + GradleUtil.getBuildIdentifier(project))

            minifyEnabled shouldRunProguard
            proguardFile file('proguard.cfg')
            testProguardFile file('proguard.cfg')

            ext.betaDistributionReleaseNotesFilePath = CHANGELOG_PATH
            ext.betaDistributionGroupAliasesFilePath = CRASHLYTICS_BETA_GROUPS_PATH

            buildConfigField "boolean", "RELEASE", "Boolean.parseBoolean(\"false\")"
            buildConfigField "boolean", "USABILITY", GradleUtil.isUsabilityBuild(project) ? "Boolean.parseBoolean(\"true\")" : "Boolean.parseBoolean(\"false\")"
        }

        release {
            if (GradleUtil.isUsabilityBuild(project)) {
                applicationIdSuffix ("." + GradleUtil.getBuildIdentifier(project))
            }

            minifyEnabled true
            proguardFile file('proguard.cfg')
            zipAlignEnabled true
            signingConfig signingConfigs.release

            buildConfigField "boolean", "RELEASE", "true"
            buildConfigField "boolean", "USABILITY", GradleUtil.isUsabilityBuild(project) ? "true" : "false"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'

        debug {
            assets.srcDirs += '../lib/mocked/templates'
        }

        test {
            java.srcDir '../lib/ExpediaBookings/src/test/java/com/expedia/bookings/data'
            java.srcDir '../lib/ExpediaBookings/src/test/java/com/expedia/bookings/testrule'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    checkstyle {
        toolVersion = '6.14.1'
    }

    task checkstyle(type: Checkstyle) {
        configFile file('checkstyle.xml')
        source 'src/'
        include '**/*.java'

        // These files are from outside our project
        exclude (
            '**/gl/*.java',
            '**/GLTagProgressBar.java',
            '**/CustomEditText.java',
            '**/SocketActivityHierarchyServer.java',
        )

        classpath = files()
        ignoreFailures = false
    }
}

public class GradleUtil {
    static def isDefined(s) {
        return s != null && !s.isEmpty() && s != "null"
    }

    static def shouldEnableProguard(Project project) {
        def buildNumber = "${System.env.BUILD_NUMBER}"
        def ci = "${System.env.CI}"
        def boolean shouldRunProguard = false

        // If BUILD_NUMBER defined turn on proguard
        if (isDefined(buildNumber)) {
            shouldRunProguard = true
        }

        // If CI defined turn on proguard
        if (isDefined(ci)) {
            shouldRunProguard = true
        }

        // This setting supercedes BUILD_NUMBER
        if (project.hasProperty("runProguard")) {
            shouldRunProguard = project.getProperty("runProguard").toBoolean()
        }

        return shouldRunProguard;
    }

    static def getPropertyWithDefault(Project project, String key, String thedefault) {
        if (project.hasProperty(key)) {
            return project.getProperty(key).toString()
        }

        return thedefault
    }

    static def getBuildIdentifier(Project project) {
        return getPropertyWithDefault(project, "id", "debug")
    }

    static def getAppName(Project project, variant) {
        // Don't touch release builds
        if (variant.buildType.name == "release" || isUsabilityBuild(project)) {
            return null
        }

        def flavor = variant.productFlavors.get(0).name
        flavor = flavor.capitalize()

        def type = variant.buildType.name
        if (type == "debug") {
            type = getBuildIdentifier(project)
        }
        type = type.capitalize()

        def appname = flavor + " " + type
        return appname.tr('_', '.')
    }

    static def isUsabilityBuild(Project project) {
        return getBuildIdentifier(project) == "usability"
    }

    static def getGitRevision() {
        def hash = "git rev-parse --short HEAD".execute().text.trim()
        def hasLocalChanges = "git diff --raw".execute().text
        if (hasLocalChanges) {
            hash = "!" + hash
        }
        return hash
    }

    static def versionCode(major, minor, patch, build) {
        return major * 1000000 + minor * 10000 + patch * 100 + build
    }

    static def versionName(major, minor, patch, build) {
        def name = "${major}.${minor}.${patch}"
        if (build > 0) {
            name = "${name}.${build}"
        }
        return name
    }
}

def releaseAccountTokens = [
    'com.expedia.bookings' : 'expedia.tuid',
    'com.expedia.bookings.vsc' : 'vsc.tuid',
    'com.travelocity.android' : 'travelocity.tuid',
]

android.applicationVariants.all { variant ->
    // We released apps with the following tokens so we should maintain them so people don't get logged out
    def token = releaseAccountTokens[variant.applicationId]
    if (!GradleUtil.isDefined(token)) {
        token = "${variant.applicationId}.tuid"
    }
    variant.resValue "string", "expedia_account_token_type_tuid_identifier", "${token}"
    variant.resValue "string", "expedia_account_type_identifier", "${variant.applicationId}"

    variant.resValue "string", "authority_autocomplete_hotel", "${variant.applicationId}.autocomplete.hotel"
    variant.resValue "string", "authority_autocomplete_airport", "${variant.applicationId}.autocomplete.airport"
    variant.resValue "string", "authority_autocomplete_suggestions", "${variant.applicationId}.suggestions"
    variant.resValue "string", "authority_account_sync", "${variant.applicationId}.account.sync"

    def launcherName = GradleUtil.getAppName(project, variant)
    if (!GradleUtil.isDefined(launcherName)) {
        // Must be a release build, so we default to app_name
        launcherName = "@string/app_name"
    }
    variant.resValue "string", "launcher_name", "${launcherName}"
}

