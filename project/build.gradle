import java.util.regex.Pattern

def isServerBuild
def jenkinsBuildNumber = "$System.env.BUILD_NUMBER"
if (!jenkinsBuildNumber.isEmpty() && jenkinsBuildNumber != "null") {
    isServerBuild = true
} else {
    isServerBuild = false
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
    }
}
apply plugin: 'android'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile project(':lib:ActionBarSherlock:actionbarsherlock')
    compile project(':lib:FacebookSdk:facebook')
    compile project(':lib:FlightLib')
    compile project(':lib:google-play-services')
    compile project(':lib:OpinionLab')
    compile project(':lib:svg-android:svgandroid')
    compile project(':lib:Utils')
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.0.1"

    defaultConfig {
        versionCode 24
        versionName "3.3"
        minSdkVersion 8
        targetSdkVersion 18
    }

    productFlavors {
        EBad {
            buildConfig "public static final boolean IS_VSC = false;"
        }

        VSC {
            buildConfig "public static final boolean IS_VSC = true;"
            packageName "com.expedia.bookings.vsc"
            versionCode 1
            versionName "1.0"
        }
    }

    buildTypes {
        alpha.initWith(buildTypes.debug)
        alpha {
            packageNameSuffix ".alpha"

            runProguard isServerBuild
            proguardFile 'proguard.cfg'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        instrumentTest.setRoot('tests')

        release {
            res.srcDirs 'res_release'
        }

        alpha {
            manifest.srcFile 'AndroidManifest-alpha.xml'
            res.srcDirs 'res_alpha'
        }

        VSC {
            res.srcDirs 'res_vsc'
        }

        signingConfigs {
            debug {
                storeFile file("../common/debug.keystore")
            }
        }
    }

}

if (isServerBuild) {
    def manifestFile = file("AndroidManifest-alpha.xml")
    def pattern = Pattern.compile("android:name=\"buildNumber\"\\s*android:value=\"(\\d+)\"")
    def manifestText = manifestFile.getText()
    def matcher = pattern.matcher(manifestText)
    matcher.find()
    def manifestContent = matcher.replaceAll("android:name=\"buildNumber\" android:value=\"" + jenkinsBuildNumber + "\"")
    manifestFile.write(manifestContent)
}
